cmake_minimum_required(VERSION 3.22)
project(TerminalTeamsTextBox VERSION 1.0)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED TRUE)
set(CMAKE_THREAD_PREFER_PTHREAD TRUE)
set(THREADS_PREFER_PTHREAD_FLAG TRUE)

find_package(Threads REQUIRED)

get_filename_component(TT_ROOT_DIRECTORY "../../" ABSOLUTE)
get_filename_component(TT_DIAGNOSTICS_DIRECTORY "${TT_ROOT_DIRECTORY}/diagnostics" ABSOLUTE)
get_filename_component(TT_TEXTBOX_SRC_DIRECTORY "." ABSOLUTE)
set(TT_DIAGNOSTICS_LIB "tteams-diagnostics")

if (NOT TARGET ${TT_DIAGNOSTICS_LIB})
  add_subdirectory("${TT_DIAGNOSTICS_DIRECTORY}" "${TT_DIAGNOSTICS_LIB}")
endif()

set(TT_TEXTBOX_SCRIPTS
  "${TT_TEXTBOX_SRC_DIRECTORY}/tteams-textbox.sh"
)
foreach(TT_TEXTBOX_SCRIPT ${TT_TEXTBOX_SCRIPTS})
  configure_file("${TT_TEXTBOX_SCRIPT}" "${PROJECT_BINARY_DIR}" COPYONLY)
endforeach()

set(TT_TEXTBOX_LIB tteams-textbox)
add_library(${TT_TEXTBOX_LIB}
  "${TT_TEXTBOX_SRC_DIRECTORY}/TTTextBoxSettings.cpp"
  "${TT_TEXTBOX_SRC_DIRECTORY}/TTTextBox.cpp"
)
set_target_properties(${TT_TEXTBOX_LIB} PROPERTIES VERSION ${PROJECT_VERSION})
target_include_directories(${TT_TEXTBOX_LIB} PUBLIC "${PROJECT_BINARY_DIR}")
target_include_directories(${TT_TEXTBOX_LIB} PUBLIC "${TT_TEXTBOX_SRC_DIRECTORY}")
target_include_directories (${TT_TEXTBOX_LIB} PUBLIC $<TARGET_PROPERTY:${TT_DIAGNOSTICS_LIB},INTERFACE_INCLUDE_DIRECTORIES>)
if ("${CMAKE_BUILD_TYPE}" STREQUAL "Debug")
  target_link_libraries(${TT_TEXTBOX_LIB} ${TT_DIAGNOSTICS_LIB})
endif()

set(TT_TEXTBOX tteams-textbox-exe)
add_executable(${TT_TEXTBOX}
  "${TT_TEXTBOX_SRC_DIRECTORY}/Main.cpp"
)
set_target_properties(${TT_TEXTBOX} PROPERTIES OUTPUT_NAME ${TT_TEXTBOX_LIB})
target_include_directories(${TT_TEXTBOX} PUBLIC "${PROJECT_BINARY_DIR}")
target_include_directories(${TT_TEXTBOX} PUBLIC "${TT_TEXTBOX_SRC_DIRECTORY}")
target_include_directories (${TT_TEXTBOX} PUBLIC $<TARGET_PROPERTY:${TT_DIAGNOSTICS_LIB},INTERFACE_INCLUDE_DIRECTORIES>)
target_link_libraries(${TT_TEXTBOX} ${TT_TEXTBOX_LIB})

set(TT_TEXTBOX_HANDLER_LIB tteams-textbox-handler)
add_library(${TT_TEXTBOX_HANDLER_LIB}
  "${TT_TEXTBOX_SRC_DIRECTORY}/TTTextBoxHandler.cpp"
)
target_include_directories(${TT_TEXTBOX_HANDLER_LIB} PUBLIC "${PROJECT_BINARY_DIR}")
target_include_directories(${TT_TEXTBOX_HANDLER_LIB} PUBLIC "${TT_TEXTBOX_SRC_DIRECTORY}")
target_include_directories (${TT_TEXTBOX_HANDLER_LIB} PUBLIC $<TARGET_PROPERTY:${TT_DIAGNOSTICS_LIB},INTERFACE_INCLUDE_DIRECTORIES>)

install(TARGETS ${TT_TEXTBOX} DESTINATION "bin")
install(TARGETS ${TT_TEXTBOX_LIB} DESTINATION "lib")
install(TARGETS ${TT_TEXTBOX_HANDLER_LIB} DESTINATION "lib")
install(FILES ${TT_TEXTBOX_SCRIPTS} DESTINATION "bin"
  PERMISSIONS
    OWNER_EXECUTE OWNER_WRITE OWNER_READ
    GROUP_EXECUTE GROUP_READ
    WORLD_EXECUTE WORLD_READ
)
